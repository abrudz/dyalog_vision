 Rho←{ ⍝ ⍴ autosize: 0.5 (shorten) 1.5 (recycle) 2.5 (pad) and allowing reversal of axis by negation of length
     0::∆SIGNAL ⎕DMX
     0=⎕NC'⍺':⍴⍵ ⍝ monadic case
     m←⍺≠⌊⍺ ⍝ find the special dimension, if any
     1<+/m:⎕SIGNAL⊂('EN' 11)('Message' 'At most one shape element can be fractional')
     3<|m+.×⍺:⎕SIGNAL⊂('EN' 11)('Message' 'The fractional shape element must be between ¯3 and 3')
     n←(≢,⍵)÷×/(~m)/|⍺ ⍝ the divisor of the shape
     t←|⊃⍬⍴m/⍺
     n←⌈⌊⍣(0.5=t)⊢n
     s←n@{m}|⍺
     z←s××⍺
     ⊃{(⌽[|⍺]⍣(⍺<0))⍵}/((⍳⍤≢××)z),⊂(|z)⍴(×/s)↑⍣(2.5=t),⍵
 }
